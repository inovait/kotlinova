/*
 * Copyright 2020 INOVA IT d.o.o.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

apply plugin: 'maven'
apply plugin: 'maven-publish'

static def getMavenSettingsCredentials() {
    String mavenSettingsFileName = System.getenv("MAVEN_PUBLISH_SETTINGS")
    if (mavenSettingsFileName == null) {
        return null
    }
    File mavenSettings = new File(mavenSettingsFileName)
    if (!mavenSettings.exists()) {
        return null
    }

    def xmlSlurper = new XmlSlurper()
    def output = xmlSlurper.parse(mavenSettings)
    return output."servers"."server"
}

def getCredentials(String server) {
    def entries = getMavenSettingsCredentials()

    if (entries == null) {
        return [username: ".m2/settings.xml missing", password: "No password"]
    }

    for (entry in entries) {
        if ( entry."id".text() == server ) {
            return [username: entry.username.text(), password: entry.password.text()]
        }
    }
}

static def getReleaseRepositoryUrl() {
    return "http://nexus/content/repositories/releases"
}

static def getSnapshotRepositoryUrl() {
    return "http://nexus/content/repositories/snapshots"
}

// Make sure output aar name is consistent
if (project.isAndroid()) {
    project.android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "output.aar"
        }
    }
}

if (project.isAndroid()) {
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
} else {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java.srcDirs
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "si.inova.kotlinova"
            artifactId project.POM_ARTIFACT_ID
            // Automatically append snapshot
            // to local maven builds for easier testing
            version "${VERSION}-SNAPSHOT"

            if (project.isAndroid()) {
                artifact(androidSourcesJar)
                artifact("$buildDir/outputs/aar/output.aar")
            } else {
                artifact(sourcesJar)
                artifact(jar)
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.groupId = "si.inova.kotlinova"
                pom.artifactId = project.POM_ARTIFACT_ID
                pom.version = VERSION

                repository(url: getReleaseRepositoryUrl()) {
                    def credentials = getCredentials("releases")
                    authentication(userName: credentials["username"], password: credentials["password"])
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    def credentials = getCredentials("snapshots")
                    authentication(userName: credentials["username"], password: credentials["password"])
                }

                pom.project {
                    name project.POM_ARTIFACT_ID
                    if (project.isAndroid()) {
                        packaging 'aar'
                    }
                    url 'http://hydra/utilities/kotlinova/kotlinova'

                    scm {
                        url 'http://hydra/utilities/kotlinova'
                    }
                }
            }
        }
    }

    if (project.isAndroid()) {
        artifacts {
            archives androidSourcesJar
        }

        tasks.publishMavenPublicationToMavenLocal.dependsOn(assembleRelease)
    } else {
        artifacts {
            archives sourcesJar
        }

        tasks.publishMavenPublicationToMavenLocal.dependsOn(jar)
    }
}