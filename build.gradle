/*
 * Copyright 2020 INOVA IT d.o.o.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

buildscript {
    apply from: 'libraries.gradle'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-noarg:${versions.kotlin}"
        classpath "de.mobilej.unmock:UnMockPlugin:${versions.unmock}"
    }

    def versions = new Properties()
    file("version.properties").withInputStream { versions.load(it) }

    ext {
        MAJOR_VERSION = versions.getProperty("MAJOR")
        MINOR_VERSION = versions.getProperty("MINOR")
        PATCH_VERSION = versions.getProperty("PATCH")

        VERSION = "${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}"
    }
}

subprojects {
    ext.isAndroid = {
        return project.hasProperty("android")
    }

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('com.github.inovait.kotlinova:core') with project(':core')
                substitute module('com.github.inovait.kotlinova:rx') with project(':rx')
                substitute module('com.github.inovait.kotlinova:android') with project(':android')
                substitute module('com.github.inovait.kotlinova:gms') with project(':gms')
                substitute module('com.github.inovait.kotlinova:retrofit') with project(':retrofit')
                substitute module('com.github.inovait.kotlinova:room') with project(':room')
            }
        }
    }

    afterEvaluate {
        if (project.isAndroid()) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8

                    kotlinOptions {
                        jvmTarget = "1.8"
                    }
                }

                defaultConfig {
                    multiDexEnabled true
                }
            }
        } else {
            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
                kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
        }


        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
        }

        // Some experimental APIs are used on the testing side to ease testing with coroutines
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
        }

        allOpen {
            annotation("si.inova.kotlinova.testing.OpenForTesting")
        }

        if (it.plugins.hasPlugin("de.mobilej.unmock")) {
            unMock {
                keep "android.widget.BaseAdapter"
                keep "android.widget.ArrayAdapter"
                keep "android.os.Bundle"
                keepStartingWith "android.database.MatrixCursor"
                keep "android.database.AbstractCursor"
                keep "android.database.CrossProcessCursor"
                keepStartingWith "android.text.TextUtils"
                keepStartingWith "android.util."
                keepStartingWith "android.text."
                keepStartingWith "android.content.ContentValues"
                keepStartingWith "android.content.ComponentName"
                keepStartingWith "android.content.ContentUris"
                keepStartingWith "android.content.ContentProviderOperation"
                keepStartingWith "android.content.ContentProviderResult"
                keepStartingWith "android.content.UriMatcher"
                keepStartingWith "android.content.Intent"
                keep "android.location.Location"
                keepStartingWith "android.content.res.Configuration"
                keepStartingWith "org."
                keepStartingWith "libcore."
                keepStartingWith "com.android.internal.R"
                keepStartingWith "com.android.internal.util."
                keep "android.net.Uri"

                keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
            }

            dependencies {
                unmock "org.robolectric:android-all:${versions.unmockAndroidVersion}"
            }
        }
    }
}

apply from: 'ci.gradle'
